@DSL DefaultGenericBehaviour;
@Author Maxime PIERRE;
@Behaviour PoroElasticity_Maxime;

@ExternalStateVariable stress pₗ;
pₗ.setEntryName("LiquidPressure");

@Gradient StrainStensor εᵗᵒ;
εᵗᵒ.setGlossaryName("Strain");
@Gradient TVector ∇pₗ;
∇pₗ.setEntryName("LiquidPressureGradient");

@Flux StressStensor σ;
σ.setGlossaryName("Stress");
@Flux TVector w;
w.setEntryName("FluidFlux");

@MaterialProperty stress E;
E.setGlossaryName("YoungModulus");
@MaterialProperty real ν;
ν.setGlossaryName("PoissonRatio");
@MaterialProperty real b;
b.setEntryName("BiotCoefficient");
@MaterialProperty stress N_biot;
N_biot.setEntryName("BiotN");
@MaterialProperty real kₗ;
kₗ.setEntryName("LiquidPermeability");
@MaterialProperty real φ₀;
φ₀.setEntryName("InitialPorosity");

@StateVariable real φₗ ;
φₗ.setEntryName("PorosityVariation");
@StateVariable real φ ;
φ.setGlossaryName("Porosity");
@StateVariable real mₗ ;
mₗ.setEntryName("LiquidMass");
@StateVariable real ρₗ ;
ρₗ.setEntryName("LiquidDensity");


@StaticVariable stress Kᶠ = 2.1e9;
//Kᶠ.setEntryName("WaterBulkModulus");
@StaticVariable real ηₗ = 1e-3;
//ηₗ.setEntryName("WaterViscosity");

@LocalVariable stress λ;
@LocalVariable stress μ;

@TangentOperatorBlocks{∂σ∕∂Δεᵗᵒ, ∂σ∕∂Δpₗ, ∂mₗ∕∂Δεᵗᵒ, ∂mₗ∕∂Δpₗ, ∂w∕∂Δpₗ, ∂w∕∂Δ∇pₗ};

@Integrator{
    λ = computeLambda(E, ν);
    μ = computeMu(E, ν);
    σ += λ⋅trace(Δεᵗᵒ)⋅I₂ + 2⋅μ⋅Δεᵗᵒ - b⋅Δpₗ⋅I₂ ;
  
    φₗ += b⋅trace(Δεᵗᵒ) + 1/N_biot⋅Δpₗ ;

    φ = φₗ + φ₀ ;
    ρₗ = (1+(pₗ+Δpₗ)/Kᶠ)⋅1000.0 ;
    mₗ = ρₗ⋅φ ;
    w = -ρₗ/ηₗ⋅kₗ⋅(∇pₗ + Δ∇pₗ) ;
}

@TangentOperator{
    ∂σ∕∂Δεᵗᵒ = λ⋅(I₂ ⊗ I₂) + 2⋅μ⋅I₄;
    ∂σ∕∂Δpₗ   = - b⋅I₂ ;
    ∂mₗ∕∂Δεᵗᵒ = ρₗ⋅b⋅I₂ ;
    ∂mₗ∕∂Δpₗ   = 1000.0/Kᶠ⋅φ + ρₗ/N_biot ;
    ∂w∕∂Δpₗ   = -1000.0/Kᶠ/ηₗ⋅kₗ⋅(∇pₗ + Δ∇pₗ) ;
    ∂w∕∂Δ∇pₗ  = -ρₗ/ηₗ⋅kₗ⋅tmatrix<N, N, real>::Id();;
}
